define intSym @"intermediate-symbols.a";
define boundary ⟪MB⟫|⟪RB⟫|⟪IGB⟫|intSym;

! --------------------------------------------------------------------------
! the verb roots 'de-' and 'ye-' becom di- and yi- if a suffix
! starting with a 'y' follows
! --------------------------------------------------------------------------
regex e -> i || .#. [d|y]  _  boundary+ (⟪bI⟫|⟪bA⟫) [⟪by⟫|y]
   ,, e -> e || .#. d  _  boundary+ ⟪by⟫ ⟪I⟫ p [⟪MB⟫|⟪IGB⟫]
! --------------------------------------------------------------------------
! -yor exception - `a' `e' and underspecified low-unrounded vowel `A' becomes
! corresponding high vowel before the suffix -yor
! --------------------------------------------------------------------------
   ,, [a|e|⟪A⟫] -> ⟪I⟫ ||  _  boundary+ ⟪bI⟫ y o r [⟪MB⟫|⟪IGB⟫]
! ------------------------------------------------------------
! Pronoun exceptions
! - drop of final `i/ı' after içeri/dışarı
! - drop of final `a/e' after ora/bura/şura/nere
! ------------------------------------------------------------
    ,, e -> a || .#. [s|b] _ n ⟪prn⟫ intSym* ⟪RB⟫ [⟪IGB⟫|⟪MB⟫]* ⟪by⟫ ⟪A⟫ [⟪MB⟫|⟪IGB⟫]
    ,, [ı|i] (->) 0 || .#. [{içer}|{dışar}|{iler}] _ 
                        boundary* ⟪D⟫ ⟪A⟫ (n) [⟪MB⟫|⟪IGB⟫]
    ,, [a|e] (->) 0 || .#. [{bˈur}|{şˈur}|{ˈor}|{nˈer}]  _ 
                boundary* [⟪D⟫ ⟪A⟫ (n) | ⟪by⟫ ⟪A⟫] [⟪MB⟫|⟪IGB⟫]
    ,, ⟪by⟫   -> y || .#. [{bˈura}|{şˈura}|{ˈora}|{nˈere}] 
                boundary* _ ⟪A⟫ [⟪MB⟫|⟪IGB⟫]
;

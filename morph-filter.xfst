!
! The rules below either filters out some of the overgenerated
! analysis sequences in morph.lexc, or (optionally) replaces some
! symbols with others. The lower side of the resulting transducer is
! fed to the transducers producing analysis and surface layers.
!

define intSym @"intermediate-symbols.a";
define posSym @"pos-symbols.a";

! compund verb forms
define compVerb [⟨abil⟩|⟨iver⟩|⟨adur⟩|⟨agel⟩|⟨agor⟩|⟨akal⟩|⟨ayaz⟩];



! TODO: parallelize more
regex  [⟪neg⟫|⟪ques⟫] -> 0         ! we ignore these lexical symbols for now
    ,, [⟪onom⟫|⟪emoticon⟫] -> 0
    ,, [⟪unit⟫|⟪pers⟫|⟪loc⟫|⟪nation⟫] -> 0
    ,, [⟪dem⟫|⟪pers⟫|⟪loc⟫|⟪refl⟫|⟪neg⟫] -> 0
    ,, [⟪1s⟫|⟪2s⟫|⟪3s⟫|⟪1p⟫|⟪2p⟫|⟪3p⟫] -> 0
    ,, [⟪ppadj⟫|⟪ablC⟫|⟪accC⟫|⟪datC⟫|⟪genC⟫|⟪insC⟫|⟪liC⟫|⟪nomC⟫|⟪numC⟫] -> 0
    ,, ⟪part⟫ -> 0
    .o. ~[ ?* ⟪causNone⟫ intSym* ⟨V⟩ ⟨caus⟩ ?* ]
    .o. ~[ [\[⟪rcp⟫]]+ ⟨rcp⟩ ?*]
    .o. ~[ [\[⟪rfl⟫]]+ ⟨rfl⟩ ?*]
    .o. ~[ ?* ⟨vn%.mek⟩ ⟨pl⟩ ?* ]
    .o. ~[ ?* ⟨imp⟩ ?+ [⟨2s⟩|⟨3s⟩|⟨2p⟩|⟨3p⟩] ]
    .o. ~[ ?* ⟨imp⟩ [⟨2s⟩|⟨3s⟩|⟨2p⟩|⟨3p⟩] ?+ ]
    .o. ~[ ?* ⟨prs⟩ ?+ [⟨2s⟩|⟨2p⟩] ]
    .o. ~[ ?* ⟨prs⟩ [⟨2s⟩|⟨2p⟩] ?+ ]
    .o. ~[ ?* ⟨apos⟩ ⟨sg⟩ ⟨p%0x⟩ ⟨nom⟩ ⟨cpl⟩ ⟨V⟩ ⟨3s⟩ ]
    .o. ~[ ?* ⟪compn⟫ ?* ⟨apos⟩ ⟨sg⟩ ⟨p3s⟩ ⟨nom⟩ ⟨cpl⟩ ⟨V⟩ ⟨3s⟩ ]
    .o. ~[ [\[⟪time⟫]]+ ⟨Adv⟩ [⟨ki⟩|⟨leri⟩|⟨ligine⟩] ?*]
    .o. ~[ ?* ⟪time⟫ ?* ⟨Adv⟩ [⟨ca⟩] ?*]
    .o. ~[ ?* {birbir} ⟨Pron⟩ ⟨sg⟩ ⟨p%0x⟩ ⟨nom⟩ ⟨cpl⟩ ⟨3s⟩ ]
!    .o. ~[ ?* ⟨Q⟩ ⟨3p⟩ ?+ ]    ! filter out 'görmüş mülerdir'
! disallow 3 or more instances of compound verb suffixes ??
!    .o. ~[ ?* compVerb ?* compVerb ?* compVerb ?* ]
!
! disallow some tense/copular suffix combinations
!
    .o. ~[ ?* \⟨cpl⟩ ⟨past⟩ ⟨cpl⟩ ⟨evid⟩ ?* ]    ! okudukmuş
    .o. ~[ ?* \⟨cpl⟩ ⟨cond⟩ ⟨cpl⟩ ⟨cond⟩ ?* ]    ! okusaysa
    .o. ~[ ?* \⟨cpl⟩ ⟨opt⟩ ⟨cpl⟩ ⟨cond⟩ ?* ]     ! okuyaysa
    .o. ~[ ?* \⟨cpl⟩ [⟨past⟩|⟨cond⟩|⟨opt⟩] [\⟨cpl⟩]* ⟨dir⟩ ]
        ! okuduktur okusadır okuyadır
! the rules below deals with variablity of person/number markers,
! particularly 3p.
! Doing this here means that the surface/analysis forms should match.
! For fixed analysis order the same rules can be moved to
! <analysis-filter.xfst>.
    .o.⟨cpl⟩ ⟨past⟩ ⟨1s⟩ (->) ⟨1s⟩ ⟨cpl⟩ ⟨past⟩,
       ⟨cpl⟩ ⟨past⟩ ⟨2s⟩ (->) ⟨2s⟩ ⟨cpl⟩ ⟨past⟩,
       ⟨cpl⟩ ⟨past⟩ ⟨1p⟩ (->) ⟨1p⟩ ⟨cpl⟩ ⟨past⟩,
       ⟨cpl⟩ ⟨past⟩ ⟨2p⟩ (->) ⟨2p⟩ ⟨cpl⟩ ⟨past⟩,
       ⟨cpl⟩ ⟨cond⟩ ⟨1s⟩ (->) ⟨1s⟩ ⟨cpl⟩ ⟨cond⟩,
       ⟨cpl⟩ ⟨cond⟩ ⟨2s⟩ (->) ⟨2s⟩ ⟨cpl⟩ ⟨cond⟩,
       ⟨cpl⟩ ⟨cond⟩ ⟨1p⟩ (->) ⟨1p⟩ ⟨cpl⟩ ⟨cond⟩,
       ⟨cpl⟩ ⟨cond⟩ ⟨2p⟩ (->) ⟨2p⟩ ⟨cpl⟩ ⟨cond⟩
       || ⟨past⟩ _
    ,, ⟨cpl⟩ ⟨past⟩ ⟨3p⟩ (->) ⟨cpl⟩ ⟨3p⟩ ⟨past⟩
    ,, ⟨cpl⟩ ⟨past⟩ ⟨3p⟩ (->) ⟨cpl⟩ ⟨3p⟩ ⟨past⟩
    ,, ⟨cpl⟩ ⟨evid⟩ ⟨3p⟩ (->) ⟨cpl⟩ ⟨3p⟩ ⟨evid⟩
    ,, ⟨cpl⟩ ⟨cond⟩ ⟨3p⟩ (->) ⟨cpl⟩ ⟨3p⟩ ⟨cond⟩
    ,, ⟨cpl⟩ "⟨cv.ken⟩" ⟨3p⟩ (->) ⟨cpl⟩ ⟨3p⟩ "⟨cv.ken⟩"
    ,, ⟨cpl⟩ ⟨V⟩ ⟨past⟩ ⟨3p⟩ (->)     ⟨cpl⟩ ⟨V⟩  ⟨3p⟩ ⟨past⟩
    ,, ⟨cpl⟩ ⟨V⟩ ⟨past⟩ ⟨3p⟩ (->)     ⟨cpl⟩ ⟨V⟩  ⟨3p⟩ ⟨past⟩
    ,, ⟨cpl⟩ ⟨V⟩ ⟨evid⟩ ⟨3p⟩ (->)     ⟨cpl⟩ ⟨V⟩  ⟨3p⟩ ⟨evid⟩
    ,, ⟨cpl⟩ ⟨V⟩ ⟨cond⟩ ⟨3p⟩ (->)     ⟨cpl⟩ ⟨V⟩  ⟨3p⟩ ⟨cond⟩
    ,, ⟨cpl⟩ ⟨V⟩ "⟨cv.ken⟩" ⟨3p⟩ (->) ⟨cpl⟩ ⟨V⟩  ⟨3p⟩ "⟨cv.ken⟩"
    ,, ⟨3p⟩ ⟨dir⟩       (->) ⟨dir⟩  ⟨3p⟩
   .o. ⟨q⟩ ⟨cpl⟩ ⟨3p⟩   (->) ⟨3p⟩ ⟨q⟩ ⟨cpl⟩
    ,, [..]             (->) ⟨3p⟩   || ⟨3p⟩ ⟨dir⟩ _
    ,, ⟨q⟩ ⟨1s⟩ -> ⟨1s⟩ ⟨q⟩,
       ⟨q⟩ ⟨2s⟩ -> ⟨2s⟩ ⟨q⟩,
       ⟨q⟩ ⟨3s⟩ -> ⟨3s⟩ ⟨q⟩,
       ⟨q⟩ ⟨1p⟩ -> ⟨1p⟩ ⟨q⟩,
       ⟨q⟩ ⟨2p⟩ -> ⟨2p⟩ ⟨q⟩ || [⟨opt⟩|⟨cond⟩|⟨past⟩] _
   ! Türkiye Cumhuriyet-i'ne
   ,,  [..] (->) ⟨apos⟩ || [⟨p3s⟩|⟨p3p⟩] _ 
!   .o. ~[ ?* ⟨apos⟩ (⟨sg⟩ ⟨p3s⟩) ⟨nom⟩ ⟨cpl⟩ ⟨V⟩ ⟨3s⟩ ]
   .o. ⟨q⟩ ⟨3p⟩ (->) ⟨3p⟩ ⟨q⟩ 
   .o. [..] (->) ⟪mredup⟫
       || .#. _ ?+ 
            [posSym - [⟨Q⟩|⟨Ij⟩|⟨Alpha⟩|⟨Sym⟩|⟨Punc⟩
                       |⟨Num⟩ [⟨ara⟩|⟨rom⟩|⟨date⟩|⟨time⟩]]]
! Family nouns can get possessive markers before the pulural marker
! in some cases. This is some sort of derivation, but we just analyze 
! as if the inflectins are re-ordered.
    ,, ⟨pl⟩ ⟨p1s⟩  -> ⟨p1s⟩ ⟨pl⟩,
       ⟨pl⟩ ⟨p2s⟩  -> ⟨p2s⟩ ⟨pl⟩,
       ⟨pl⟩ ⟨p3s⟩  -> ⟨p3s⟩ ⟨pl⟩
       || ⟪family⟫ intSym* ⟨N⟩ _
    ,, ⟪family⟫  -> 0
! nouns expressing time may have -ki suffixed directly
    ,, [..] (->) ⟨ki⟩ ⟨Adj⟩ || ⟪time⟫ intSym* ⟨N⟩ _
;

! This file is part of TRmorph.
!
! Released under the terms of the MIT License
! Copyright (c) 2011-2015 Çağrı Çöltekin <cagri@coltekin.net>
! 
! Permission is hereby granted, free of charge, to any person obtaining a
! copy of this software and associated documentation files (the "Software"),
! to deal in the Software without restriction, including without limitation
! the rights to use, copy, modify, merge, publish, distribute, sublicense,
! and/or sell copies of the Software, and to permit persons to whom the
! Software is furnished to do so, subject to the following conditions:
! 
! The above copyright notice and this permission notice shall be included in
! all copies or substantial portions of the Software.
! 
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
! FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
! DEALINGS IN THE SOFTWARE.
!

!
! The automata defined here is a `best-effort' conversion utility that
! converts analysis strings between Oflazer's analyzer and TRmorph format.
!
! A clean one-to-one conversion is not possible, but this code
! "mostly works" for converting output of Oflazer's analyzer to
! TRmorph. Success on the other direction may not be as good.
!
! The conversion below is mostly unambiguous, but some decisions are
! rather arbitrary.
!
! There is also a python script `o2t.py' that modifies output of is this 
! automaton to fix some aspects that are difficult to handly by an FST, 
! and also marks results that do not generate the same surface form
! with TRmorph for one reason or another.
!


define Oaggr %+A3pl|%+A3sg|%+A2pl|%+A2sg|%+A1pl|%+A1sg; 
define Ocase %+Abl|%+Acc|%+Dat|%+Gen|%+Ins|%+Loc|%+Nom|%+Equ; 

define Tnpos %<N%>|%<N%:prop%>;
define Tprnpos %<Prn%>|%<Prn%:dem%>|%<Prn%:pers%:1p%>|%<Prn%:pers%:1s%>|%<Prn%:pers%:2p%>|%<Prn%:pers%:2s%>|%<Prn%:pers%:3p%>|%<Prn%:pers%:3s%>|%<Prn%:qst%>|%<Prn%:refl%:1p%>|%<Prn%:refl%:1s%>|%<Prn%:refl%:2p%>|%<Prn%:refl%:2s%>|%<Prn%:refl%:3p%>|%<Prn%:refl%:3s%>;

define Otam %+Cond|%+Opt|%+Imp|%+Prog2|%+Neces|%+Desr|%+Past|%+Fut|%+Narr|%+Aor|%+Pres|%+Prog1;

define ucase [A|B|C|Ç|D|E|F|G|Ğ|H|I|İ|J|K|L|M|N|O|Ö|P|R|S|Ş|T|U|Ü|V|Y|Z|Î|Û|Â|X|Q|W];
define lcase [a|b|c|ç|d|e|f|g|ğ|h|ı|i|j|k|l|m|n|o|ö|p|r|s|ş|t|u|ü|v|y|z|î|û|â|x|q|w];

! words that marked as a noun, but we mark them as adjectives.
define n2j {asıl}|
           {süper}|
           {yabancı}|
           {çeyrek}|
           {birleşik}|
           {yiğit}|
           {tutuksuz}|
           {transparan}|
           {sarhoş}|
           {sakat}|
           {köktendinci}|
           {fakir}|
           {yenik}|
           {narkotik}|
           {merhum}|
           {tuhaf}|
           {dindar}|
           {kırık}|
           {tamam}|
           {saçma}|
           {radikal}|
           {makro}|
           {hayran}|
           {ünlü}|
           {meşhur}|
           {layık}|
           {tıpkı}|
           {demokrat}|
           {komik}|
           {yakın}|
           {genç}|
           {liberal}|
           {saf}|
           {pembe}|
           {kör}|
           {yarı}|
           {hasta}|
           {yoksul}|
           {canlı}|
           {yanlış}|
           {merkez}|
           {muhtaç}|
           {profesyonel}|
           {tutuklu}|
           {haberdar}|
           {galip}|
           {muhafazakar}|
           {sahip}|
           {rahat}|
           {alternatif};

define liAdj  {asıl}|
              {kök}|
              {batı}|
              {doğu}|
              {ilgi}|
              {sevgi}|
              {yetki}|
              {parti};

define likJN  {sorumlu}|{dost}|{lider}|{özel}|{genç}|{aracı}|{açık}|{yolcu}|{dost}|{fedakar}|{öncü};
define likNN  {başbakan}|{başkan};

define abbrfilter A -> a, B -> b, C -> c, Ç -> ç, D -> d, E -> e,
                  F -> f, G -> g, Ğ -> ğ, H -> h, I -> ı, İ -> i,
                  J -> j, K -> k, L -> l, M -> m, N -> n, O -> o,
                  Ö -> ö, P -> p, R -> r, S -> s, Ş -> ş, T -> t,
                  U -> u, Ü -> ü, V -> v, Y -> y, Z -> z, Î -> î,
                  Û -> û, Â -> â, X -> x, Q -> q, W -> w;



define o2t %+Punc      ->  %<Punc%> 

        .o. %+Noun %+NotAbleState   -> %<abil%> %<V%> %<neg%> %<aor%> %<Adj%> %<lik%> %<N%>
        .o. %+Noun %+NotState       -> %<neg%> %<aor%> %<Adj%> %<lik%> %<N%>
        .o. %+Noun %+ActOf          -> %<vn%:inf%> %<N%> %<ca%> %<N%>
        .o. %+Noun %+Agt -> %<yici%> %<Adj%>    || %+Verb \%^DB* %^DB _
        .o. %+Noun %+Agt %+Prop     -> %<ci%> %<N%:prop%>      
        .o. %+Noun %+Agt            -> %<ci%> %<N%>      
        .o. %+Dim %+Noun            -> %+Dim
        .o. %+Noun %+Dim            -> %<dim%> %<N%>     
        .o. %+Noun %+FeelLike       -> %<esi%> %<Adj%>   
        .o. %+Noun %+FutPart        -> %<vn%:fut%> %<N%> 
        .o. %+Noun %+Inf1 %+Prop    -> %<vn%:inf%> %<N%:prop%> 
        .o. %+Noun %+Inf1           -> %<vn%:inf%> %<N%> 
        .o. %+Noun %+Inf2 %+Prop    -> %<vn%:inf%> %<N%:prop%> 
        .o. %+Noun %+Inf2           -> %<vn%:inf%> %<N%> 
        .o. %+Noun %+Inf            -> %<vn%:inf%> %<N%> 
        .o. %+Noun %+Inf3 %+Prop    -> %<vn%:yis%> %<N%:prop%> 
        .o. %+Noun %+Inf3           -> %<vn%:yis%> %<N%> 
        .o. %+Noun %+PastPart       -> %<vn%:past%> %<N%>
        .o. %+Noun %+PresPart       -> %<vn%:pres%> %<N%>
        .o. %+Noun %+Ness           -> %<lik%> %<N%>     
        .o. %+Noun %+Zero           -> %<%0%> %<N%>      
        .o. %+Noun %+Prop           -> %<N%:prop%>       
        .o. %+Noun %+Time           -> %<N%>             

        .o. %+Noun%+Prop            -> %<N%:prop%>
        .o. %+Noun                  -> %<N%>


        .o. %+Pron %+Rel -> %<ki%> %<Adj%> %<%0%> %<N%>

        .o. %+Pron %+Quant %+A1pl %+P1pl -> %<Det%:indef%> %<p1p%> %<Prn%>   
        .o. %+Pron %+Quant %+A2pl %+P2pl -> %<Det%:indef%> %<p2p%> %<Prn%>   
        .o. %+Pron %+Quant %+A3pl %+P3pl -> %<Det%:indef%> %<p3p%> %<Prn%>   
        .o. %+Pron %+Quant %+A3sg %+P3sg -> %<Det%:indef%> %<p3s%> %<Prn%>   

        ! TODO: the following is useless, left side is identical with the above

        .o. %+Pron %+Quant %+A1pl %+P1pl -> %<Det%:def%> %<p1p%> %<Prn%>
        .o. %+Pron %+Quant %+A2pl %+P2pl -> %<Det%:def%> %<p2p%> %<Prn%>
        .o. %+Pron %+Quant %+A3pl %+P3pl -> %<Det%:def%> %<p3p%> %<Prn%>
        .o. %+Pron %+Quant %+A3sg %+P3sg -> %<Det%:def%> %<p3s%> %<Prn%>

        .o. %+Pron %+Demons        -> %<Prn%:dem%>
        .o. %+Pron %+Ques          -> %<Prn%:qst%>
        .o. %+Pron %+Pers %+A1sg   -> %<Prn%:pers%:1s%>
        .o. %+Pron %+Pers %+A2sg   -> %<Prn%:pers%:2s%>
        .o. %+Pron %+Pers %+A3sg   -> %<Prn%:pers%:3s%>
        .o. %+Pron %+Pers %+A1pl   -> %<Prn%:pers%:1p%>
        .o. %+Pron %+Pers %+A2pl   -> %<Prn%:pers%:2p%>
        .o. %+Pron %+Pers %+A3pl   -> %<Prn%:pers%:3p%>
        .o. %+Pron %+Reflex %+A1sg -> %<Prn%:refl%:1s%>
        .o. %+Pron %+Reflex %+A2sg -> %<Prn%:refl%:2s%>
        .o. %+Pron %+Reflex %+A3sg -> %<Prn%:refl%:3s%>
        .o. %+Pron %+Reflex %+A1pl -> %<Prn%:refl%:1p%>
        .o. %+Pron %+Reflex %+A2pl -> %<Prn%:refl%:2p%>
        .o. %+Pron %+Reflex %+A3pl -> %<Prn%:refl%:3p%>
        .o. %+Pron                 -> %<Prn%>

        .o. %+A3pl -> %<pl%>, %+A3sg -> 0 || Tnpos|Tprnpos _

        .o. %+Abl -> %<abl%>
        .o. %+Acc -> %<acc%>
        .o. %+Dat -> %<dat%>
        .o. %+Gen -> %<gen%>
        .o. %+Ins -> %<ins%>
        .o. %+Loc -> %<loc%>
        .o. %+Nom -> 0
        .o. %+Equ -> %<ca%> %<Adv%>

        .o. %+P1sg -> %<p1s%>
        .o. %+P2sg -> %<p2s%>
        .o. %+P3sg -> %<p3s%>
        .o. %+P1pl -> %<p1p%>
        .o. %+P2pl -> %<p2p%>
        .o. %+P3pl -> %<p3p%>
        .o. %+Pnon -> 0

        .o. %+Adj -> %<Det%:def%> || .#. {diğer} _

        .o. %+Adj %+Agt       -> %<ci%> %<Adj%>
        .o. %+Adj %+AsIf      -> %<ca%> %<Adj%>
        .o. %+Adj %+FeelLike  -> %<esi%> %<Adj%>
        .o. %+Adj %+FitFor    -> %<lik%> %<Adj%>
        .o. %+Adj %+InBetween -> %<arasi%> %<Adj%>
        .o. %+Adj %+JustLike  -> %<imsi%> %<Adj%>
        .o. %+Adj %+PresPart  -> %<part%:pres%> %<Adj%>
        .o. %+Adj %+FutPart   -> %<part%:fut%> %<Adj%>
        .o. %+Adj %+PastPart  -> %<part%:past%> %<Adj%>
        .o. %+Adj %+Related   -> %<sal%> %<Adj%>
        .o. %+Adj %+Rel       -> %<ki%> %<Adj%>
        .o. %+Adj %+With      -> %<li%> %<Adj%>
        .o. %+Adj %+Without   -> %<siz%> %<Adj%>
        .o. %+Adj %+Zero      -> %<%0%> %<Adj%>

        .o. %+Adj             -> %<Adj%> 
! numbers
        .o. [..] -> %<perc%>  || _ [%0|1|2|3|4|5|6|7|8|9|%.|%,]* %+Percent
        .o. %+Percent  -> 0
        .o. %+Num   -> %<Num%:ara%> || [%0|1|2|3|4|5|6|7|8|9] _
        .o. %+Num   -> %<Num%>      || \[%0|1|2|3|4|5|6|7|8|9] _
        .o. %+Card  -> 0
        .o. %+Dist  -> %<dist%> 
        .o. %+Ord   -> %<ord%>
        .o. %+Real  -> 0
        .o. %+Range -> 0
        .o. %+Ratio -> 0

! adverbs
        .o. %+Adverb %+Adamantly     -> %<cv%:esiye%> %<Adv%>
        .o. %+Adverb %+AfterDoingSo  -> %<cv%:ip%> %<Adv%>
        .o. %+Adverb %+AsIf          -> %<cv%:cesine%> %<Adv%>
        .o. %+Adverb %+AsLongAs      -> %<cv%:dikce%> %<Adv%>
        .o. %+Adverb %+Ly            -> %<ca%> %<Adv%>
        .o. %+Adverb %+SinceDoingSo  -> %<cv%:eli%> %<Adv%>
        .o. %+Adverb %+When          -> %<cv%:ince%> %<Adv%>
        .o. %+Adverb %+While         -> %<cv%:ken%> %<Adv%>
        .o. %+Adverb %+ByDoingSo     -> %<cv%:erek%> %<Adv%>
        .o. %+Adverb %+Since         -> %<dir%> %<Adv%>

        .o. %+Adverb %+WithoutBeingAbleToHaveDoneSo -> %<neg%> %<abil%> %<V%> %<cv%:den%> %<Adv%>
        .o. %+Adverb %+WithoutHavingDoneSo  -> %<neg%> %<cv%:den%> %<Adv%>

        .o. %+Adverb -> %<Adv%>

! verbs
        .o. %+Verb %+Become    -> %<las%> %<V%>
        .o. %+Verb %+Acquire   -> %<lan%> %<V%>
        .o. %+Verb %+EverSince -> %<agel%> %<V%>

        .o. %+Verb %+Reflex    -> %<rfl%> %<V%>
        .o. %+Verb %+Recip     -> %<rcp%> %<V%>
        .o. %+Verb %+Caus      -> %<caus%> %<V%>
        .o. %+Verb %+Pass      -> %<pass%> %<V%>


        .o. %+Verb %+Able      -> %<abil%> %<V%>
        .o. %+Verb %+Hastily   -> %<iver%> %<V%>
        .o. %+Verb %+Stay      -> %<akal%> %<V%>
        .o. %+Verb %+Repeat    -> %<adur%> %<V%>
        .o. %+Verb %+EverSince -> %<agel%> %<V%>

        .o. %+Past               ->  %<cpl%:past%>          || %+Verb (%+Zero) _
        .o. %+Pres               ->  %<cpl%:pres%>          || %+Verb (%+Zero) _
        .o. %+Cond               ->  %<cpl%:cond%>          || %+Verb (%+Zero) _
        .o. %+Narr               ->  %<cpl%:evid%>          || %+Verb (%+Zero) _
        .o. %+Pres               ->  %<cpl%:pres%>          || %+Verb (%+Zero) _
        .o. %^DB %+Adverb %+While->  %<cv%:ken%> %<Adv%>    || %+Verb (%+Zero) _
        .o. %+A3pl %+Past        ->  %<3p%> %<cpl%:past%>   || %+Verb (%+Zero) _
        .o. %+A3pl %+Cond        ->  %<3p%> %<cpl%:cond%>   || %+Verb (%+Zero) _
        .o. %+A3pl %+Narr        ->  %<3p%> %<cpl%:evid%>   || %+Verb (%+Zero) _



        .o. %<cpl%:cond%>           -> %+Cond || .#. i %+Verb  _ 
        .o. %<cpl%:past%>           -> %+Past || .#. i %+Verb  _ 
        .o. %<cpl%:evid%>           -> %+Narr || .#. i %+Verb  _ 

        .o. %+Verb %+Zero  -> %<%0%> %<V%>
        .o. %+Verb         -> %<V%>
        .o. %+Neg          -> %<neg%>
        .o. %+Pos          -> 0

        .o. %+Cond  -> %<cpl%:cond%> || Otam         (Oaggr) _
        .o. %+Past  -> %<cpl%:past%> || Otam|%+Ques  (Oaggr) _
        .o. %+Narr  -> %<cpl%:evid%> || Otam|%+Ques  (Oaggr) _

        .o. %+Pres  -> 0 || %+Ques _ 

        .o. %+Cond  -> %<cond%>
        .o. %+Desr  -> %<cond%>
        .o. %+Opt   -> %<opt%>
        .o. %+Imp   -> %<imp%>
        .o. %+Prog2 -> %<impf%>
        .o. %+Neces -> %<obl%>
        .o. %+Past  -> %<past%>
        .o. %+Fut   -> %<fut%>
        .o. %+Narr  -> %<evid%>
        .o. %+Aor   -> %<aor%>
        .o. %+Prog1 -> %<cont%>


        .o. %+Cop %+A3sg -> %<3s%> %<dir%>
        .o. %+Cop %+A3pl -> %<dir%> %<3p%>

        .o. %+Cop -> %<dir%>


! question particle
              
        .o. %+Ques -> %<Q%>

! verbal agreement
        .o. %+A1sg  -> %<1s%>
        .o. %+A2sg  -> %<2s%>
        .o. %+A3sg  -> %<3s%>
        .o. %+A1pl  -> %<1p%>
        .o. %+A2pl  -> %<2p%>
        .o. %+A3pl  -> %<3p%>

!postpositons
        .o. %+Postp %+PCAbl -> %<Postp%:adv%:ablC%>
        .o. %+Postp %+PCAcc -> %<Postp%:adv%:accC%>
        .o. %+Postp %+PCDat -> %<Postp%:adv%:datC%>
        .o. %+Postp %+PCIns -> %<Postp%:adv%:insC%>
        .o. %+Postp %+PCNom -> %<Postp%:adv%:nomC%>
        .o. %+Postp %+PCGen -> %<Postp%:adv%:genC%>

!
! conjunctions
! hem, ve, veya  make both, we pick arbitrarily
!

        .o. %+Conj -> %<Cnj%:adv%>
           || [ {ama}|{ancak}|{çünkü}|{da}|{de}|{fakat}|
                {halbuki}|{hatta}|{lakin}|{meğer}|
                {meğerse}|{örneğin}|{oysa}| {yahut}|{yani}|{zira}] _
        .o. %+Conj -> %<Cnj%:sub%>
            || [{eğer}|{şayet}|{ki}] _
        .o. %+Conj -> %<Cnj%:coo%>
!           || [{ya}|{ne}|{veya}|{ve}|{kah}|{gerek}|
!               {ister}|{değil}|{gerekse}|{ha}}|{hem}}|
!               {ila}}|{ile}] _

        .o. %+Det  -> %<Det%:def%>
            || [{o}|{bu}|{şu}|{her}|{her% bir}|{herbir}|
                {bütün}|{tüm}|{aynı}|{öte}|{öteki}|{beri}|
                {beriki}|{öbür}|{diğer}|{tek}|{hangi}] _
        .o. %+Det -> %<Det%:indef%>

        .o. %+Interj -> %<Ij%>



        .o. %+Dup -> %<Adv%:partial%>

        .o. %<N%> %+Prop -> %<N%:prop%>

!
        .o. %^DB -> 0

!
!
!        .o. A -> a, B -> b, C -> c, Ç -> ç, D -> d, E -> e,
!            F -> f, G -> g, Ğ -> ğ, H -> h, I -> ı, İ -> i,
!            J -> j, K -> k, L -> l, M -> m, N -> n, O -> o,
!            Ö -> ö, P -> p, R -> r, S -> s, Ş -> ş, T -> t,
!            U -> u, Ü -> ü, V -> v, Y -> y, Z -> z, Î -> î,
!            Û -> û, Â -> â, X -> x, Q -> q, W -> w
!            // .#. ucase lcase* _ ?* "<N:prop>"

!
! special cases
        .o. %<cond%> -> %<cpl%:cond%>,
            %<past%> -> %<cpl%:past%>,
            %<evid%> -> %<cpl%:evid%> || .#. i %<V%> _

        .o. %<Adj%> -> %<Exist%> || .#. {var} _ 
        .o. %<Adj%> -> %<Exist%:neg%> || .#. {yok} _ 
        .o. %<V%>   -> %<Not%> %<%0%> %<V%> || .#. {değil} _

        .o. {birinci} %<Num%> %<ord%> -> {bir} %<Num%> %<ord%>
        .o. {ikinci} %<Num%> %<ord%> -> {iki} %<Num%> %<ord%>
        .o. {üçüncü} %<Num%> %<ord%> -> {üç} %<Num%> %<ord%>
        .o. {dördüncü} %<Num%> %<ord%> -> {dört} %<Num%> %<ord%>
        .o. {beşinci} %<Num%> %<ord%> -> {beş} %<Num%> %<ord%>
        .o. {altıncı} %<Num%> %<ord%> -> {altı} %<Num%> %<ord%>
        .o. {yedinci} %<Num%> %<ord%> -> {yedi} %<Num%> %<ord%>
        .o. {sekizinci} %<Num%> %<ord%> -> {sekiz} %<Num%> %<ord%>
        .o. {dokuzuncu} %<Num%> %<ord%> -> {dokuz} %<Num%> %<ord%>
        .o. {onuncu} %<Num%> %<ord%> -> {on} %<Num%> %<ord%>

        .o.  l [e|a] r %<Num%> -> %<Num%> %<pl%> %<ca%> %<Adj%> || {on}|{yüz}|{bin}|{milyon} _
        
        .o. %<p3s%> -> 0    || .#. {kendi} %<Prn%:refl%:3s%> _




        .o. {ne} %<Adj%> -> {ne} %<Prn%:qst%> 
        .o. {nasıl} %<Adj%> -> {nasıl} %<Adj%:qst%> 
!
        .o. {türk} %<Adj%>          -> {Türk} %<N%:prop%>       || .#. _
        .o. {türk} %<N%>            -> {Türk} %<N%:prop%>       || .#. _
        .o. {türkçe} %<Adj%>        -> {Türkçe} %<N%:prop%>     || .#. _
        .o. {fransızca} %<N%>       -> {Fransızca} %<N%:prop%>  || .#. _
        .o. {ingilizce} %<Adj%>     -> {İngilizce} %<N%:prop%>  || .#. _
        .o. {rum} %<N%>             -> {Rum} %<N%:prop%>        || .#. _
        .o. {rus} %<N%>             -> {Rus} %<N%:prop%>        || .#. _
        .o. {sırp} %<N%>            -> {Sırp} %<N%:prop%>       || .#. _
        .o. {italyan} %<Adj%>       -> {İtalyan} %<N%:prop%>    || .#. _
        .o. {japon} %<Adj%>         -> {Japon} %<N%:prop%>    || .#. _
        .o. {gürcü} %<Adj%>         -> {Gürcü} %<N%:prop%>      || .#. _
        .o. {bulgar} %<N%>          -> {Bulgar} %<N%:prop%>     || .#. _
        .o. {ermeni} %<N%>          -> {Ermeni} %<N%:prop%>     || .#. _
        .o. {kürt} %<N%>            -> {Kürt} %<N%:prop%>       || .#. _
        .o. {yunan} %<N%>           -> {Yunan} %<N%:prop%>      || .#. _
        .o. {alman} %<N%>           -> {Alman} %<N%:prop%>      || .#. _
        .o. {fransız} %<N%>         -> {Fransız} %<N%:prop%>    || .#. _
        .o. {japon} %<N%>           -> {Japon} %<N%:prop%>      || .#. _
        .o. {italyan} %<N%>         -> {İtalyan} %<N%:prop%>    || .#. _
        .o. %<N%> -> %<N%:prop%> || .#. {islam} _
        .o. {islam} %<N%:prop%>     -> {İslam} %<N%:prop%>      || .#. _
        .o. {hıristiyan} %<N%>      -> {Hıristiyan} %<N%:prop%> || .#. _
        .o. {yahudi} %<N%>          -> {Yahudi} %<N%:prop%>     || .#. _
        .o. {kur'an} %<N%>          -> {Kur'an} %<N%:prop%>     || .#. _
        .o. {aczmendi<N>} %<N%>     -> {Aczimendi} %<N%:prop%>  || .#. _
        .o. {AtaTürk} %<N%:prop%>   -> {Atatürk} %<N%:prop%>    || .#.  _

!
        .o. {Abd} %<N%:prop%>       -> {ABD} %<N%:prop%:abbr%>      || .#. _
        .o. {Imf} %<N%:prop%>       -> {IMF} %<N%:prop%:abbr%>      || .#. _
        .o. {Öys} %<N%:prop%>       -> {ÖYS} %<N%:prop%:abbr%>      || .#. _
        .o. {Dyp} %<N%:prop%>       -> {DYP} %<N%:prop%:abbr%>      || .#. _
        .o. {Rp} %<N%:prop%>        -> {RP} %<N%:prop%:abbr%>       || .#. _
        .o. {Anap} %<N%:prop%>      -> {ANAP} %<N%:prop%:abbr%>     || .#. _
        .o. {Chp} %<N%:prop%>       -> {CHP} %<N%:prop%:abbr%>      || .#. _
        .o. {Tbmm} %<N%:prop%>      -> {TBMM} %<N%:prop%:abbr%>     || .#. _
        .o. {Ab} %<N%:prop%>        -> {AB} %<N%:prop%:abbr%>       || .#. _
        .o. {Pkk} %<N%:prop%>       -> {PKK} %<N%:prop%:abbr%>      || .#. _
        .o. {Dtp} %<N%:prop%>       -> {DTP} %<N%:prop%:abbr%>      || .#. _
        .o. {Dsp} %<N%:prop%>       -> {DSP} %<N%:prop%:abbr%>      || .#. _
        .o. {Tüsiad} %<N%:prop%>    -> {TÜSİAD} %<N%:prop%:abbr%>   || .#. _
        .o. {Yök} %<N%:prop%>       -> {YÖK} %<N%:prop%:abbr%>      || .#. _
        .o. {Kktc} %<N%:prop%>      -> {KKTC} %<N%:prop%:abbr%>     || .#. _
        .o. {Ssk} %<N%:prop%>       -> {SSK} %<N%:prop%:abbr%>      || .#. _
        .o. {Dgm} %<N%:prop%>       -> {DGM} %<N%:prop%:abbr%>      || .#. _
        .o. {Nato} %<N%:prop%>      -> {NATO} %<N%:prop%:abbr%>     || .#. _
        .o. {Öss} %<N%:prop%>       -> {ÖSS} %<N%:prop%:abbr%>      || .#. _
        .o. {Bbp} %<N%:prop%>       -> {BBP} %<N%:prop%:abbr%>      || .#. _
        .o. {Spk} %<N%:prop%>       -> {SPK} %<N%:prop%:abbr%>      || .#. _
        .o. {Aa} %<N%:prop%>        -> {AA} %<N%:prop%:abbr%>       || .#. _
        .o. {Tsk} %<N%:prop%>       -> {TSK} %<N%:prop%:abbr%>      || .#. _
        .o. {Mit} %<N%:prop%>       -> {MİT} %<N%:prop%:abbr%>      || .#. _
        .o. {Dhkp} %<N%:prop%>      -> {DHKP} %<N%:prop%:abbr%>     || .#. _
        .o. {Tl} %<N%:prop%>        -> {TL} %<N%:prop%:abbr%>       || .#. _
        .o. {Bm} %<N%:prop%>        -> {BM} %<N%:prop%:abbr%>       || .#. _
        .o. {Dp} %<N%:prop%>        -> {DP} %<N%:prop%:abbr%>       || .#. _
        .o. {tgc} %<N%>             -> {TGC} %<N%:prop%:abbr%>      || .#. _
        .o. {dp} %<N%>              -> {DP} %<N%:prop%:abbr%>       || .#. _
        .o. {Tc} %<N%:prop%>        -> {TC} %<N%:prop%:abbr%>       || .#. _
        .o. {Mgk} %<N%:prop%>       -> {MGK} %<N%:prop%:abbr%>      || .#. _
        .o. {Oecd} %<N%:prop%>      -> {OECD} %<N%:prop%:abbr%>     || .#. _
        .o. {İmkb} %<N%:prop%>      -> {İMKB} %<N%:prop%:abbr%>     || .#. _
        .o. {Tobb} %<N%:prop%>      -> {TOBB} %<N%:prop%:abbr%>     || .#. _
        .o. {jitem} %<N%>           -> {JİTEM} %<N%:prop%:abbr%>    || .#. _
        .o. {Jitem} %<N%:prop%>     -> {JİTEM} %<N%:prop%:abbr%>    || .#. _
        .o. {ımf} %<N%:prop%>       -> {IMF} %<N%:prop%:abbr%>      || .#. _
        .o. {ımf} %<N%>             -> {IMF} %<N%:prop%:abbr%>      || .#. _
        .o. {tedaş} %<N%>           -> {TEDAŞ} %<N%:prop%:abbr%>    || .#. _
        .o. {Odtü} %<N%>            -> {ODTÜ} %<N%:prop%:abbr%>     || .#. _
        .o. {Kesk} %<N%>            -> {KESK} %<N%:prop%:abbr%>     || .#. _
        .o. {kobi} %<N%>            -> {KOBİ} %<N%:abbr%>           || .#. _
        .o. {Kobi} %<N%>            -> {KOBİ} %<N%:abbr%>           || .#. _
        .o. {Tck} %<N%>             -> {TCK} %<N%:prop%:abbr%>      || .#. _
        .o. {tdk} %<N%>             -> {TDK} %<N%:prop%:abbr%>      || .#. _
        .o. {tv} %<N%>              -> {TV} %<N%:abbr%>             || .#. _
        .o. {km} %<N%>              -> {km} %<N%:abbr%>             || .#. _
        .o. {vs} %<N%>              -> {vs} %<N%:abbr%>             || .#. _
        .o. {vb} %<N%>              -> {vb} %<N%:abbr%>             || .#. _




!        {üzer} %<N%> %<p3s%> -> {üzer} %<N%> %<p3s%> %<Prn%>
        .o. l [i|ı|ü|u] %<N%>        ->  %<N%> %<li%> %<Adj%> || .#.  liAdj  _ .#.
        .o. l [i|ı|ü|u] %<N%>        ->  %<N%> %<li%> %<Adj%> %<%0%> %<N%> || .#. liAdj _ 
        .o. {yardımcı} %<N%> %<p3s%> -> {yardım} %<N%> %<ci%> %<N%> %<p3s%>
        .o. {geçen} %<Adj%>          -> {geç} %<V%> %<part%:pres%> %<Adj%>
        .o. {önce} %<N%> %<ki%> %<Adj%>  -> {önce} %<Adv%> %<ki%> %<Adj%>


        .o. {dergah} %<N%>           -> {dergâh} %<N%>      || .#. _
        .o. {kağıt} %<N%>            -> {kâğıt} %<N%>       || .#. _
        .o. {hikaye} %<N%>           -> {hikâye} %<N%>      || .#. _
        .o. {hala} %<Adv%>           -> {hâlâ} %<Adv%>      || .#. _
        .o. {hayati} %<Adj%>         -> {hayatî} %<Adj%>    || .#. _
        .o. {nihai} %<Adj%>          -> {nihaî} %<Adj%>     || .#. _
        .o. {şura} %<N%>             -> {şûra} %<Adv%>      || .#. _
        .o. {mekan} %<Adv%>          -> {mekân} %<Adv%>     || .#. _
        .o. {vekalet} %<Adv%>        -> {vekâlet} %<Adv%>   || .#. _
        .o. {dahi} %<N%>             -> {dâhi} %<Adj%>      || .#. _
        .o. {mahkum} %<Adj%>         -> {mahkûm} %<Adj%>    || .#. _
        .o. {hakim} %<N%>            -> {hâkim} %<Adj%>     || .#. _
        .o. {nikah} %<N%>            -> {nikâh} %<N%>       || .#. _
        .o. {islami} %<Adj%>         -> {islamî} %<Adj%>    || .#. _
        .o. {şikayet} %<Adj%>        -> {şikâyet} %<N%>     || .#. _
        .o. {mahkumiyet} %<N%>       -> {mahkûmiyet} %<N%>  || .#. _
        .o. {imkan} %<Adj%>          -> {imkân} %<N%>       || .#. _
        .o. {imkan} %<N%>            -> {imkân} %<N%>       || .#. _
        .o. {harekat} %<N%>          -> {harekât} %<N%>     || .#. _


        .o. [..] -> i || .#. {milletvekil}|{içiş}|{dışiş}  _ %<N%> (%<pl%>) [%<p3s%>|%<p3p%>]
        .o. k -> ğ i || .#. {işbirli}  _ %<N%> %<p3s%>
        .o. leri -> i || .#. {içiş}|{dışiş}  _ %<N%> %<pl%> %<p3s%>
        .o. [..] -> ı || .#. {cumhurbaşkan}|{yılbaş}|{işadam} _ %<N%> %<p3s%>
        .o. [..] -> u || .#. {kamuoy}                _ %<N%> %<p3s%>

        .o. %<Prn%:qst%> -> %<Prn%:qst%:locp%> || .#. {nere} _
        .o. l [i|ı|u|ü] k %<N%> -> %<N%> %<lik%> %<N%> || .#.  likNN _
        .o. l [i|ı|u|ü] k %<N%> -> %<Adj%> %<lik%> %<N%> || .#.  likJN _


        .o. {biri} -> {bir} || .#. _ %<Det%:indef%>
        .o. {hepsi} %<Det%:indef%> %<p3p%> %<Prn%> -> {hep} %<Adv%> %<p3s%> %<Prn%>
        .o. {iken} %<Adv%> -> i %<V%> %<cv%:ken%> %<Adv%>  || .#. _


        .o. %<N%> -> %<N%> %<ci%> %<N%>    || .#. {yardımcı} _

        .o. %<Adj%> -> %<Adv%> || .#. {hayli} _
        .o. %<Adj%> -> %<Det%:def%:qst%> || .#. {hangi} _
        .o. %<Adj%> -> %<Det%:indef%> || .#.  {herhangi}|{şöyle}|{bazı}|{çok}|{birtakım}|{bunca} _
        .o. %<N%> -> %<Det%:indef%> || .#. {pekçok} _
        .o. ğ u %<Adj%> -> k %<Det%:indef%> %<p3p%> %<Prn%> || .#. {ço}|{birço} _
        .o. %<N%> -> %<Prn%:pers%:3p%> || .#. {herkes} _
        .o. %<N%> -> %<Prn%> || .#. {birşey}|{kimse}|{herşey} _
        .o. %<N%> -> %<Prn%:locp%> || .#. {bura}|{ora} _

        .o. %<N%> -> %<Adv%> || .#. {hiç}|{bugün}|{hergün} _ .#.

        .o. %<N%> -> %<Adj%> || .#.  n2j _ .#. 
        .o. %<N%> -> %<Adj%> %<%0%> %<N%>|| .#.  n2j _ 


        .o. %<Adv%> -> %<Cnj%:adv%> || .#. {böylece}|{bile}|{peki}|{hele}|{dolayısıyla}|{nitekim} _ 
        .o. %<Adv%> -> %<Adv%:qst%>  || .#. {nasıl}|{niye}|{niçin} _
        .o. %<Adj%> -> %<Num%:qst%> || .#. {kaç} _

        .o. {boyunca} %<Postp%:adv%:nomC%> -> {boy} %<N%> %<p3s%> %<ca%> %<Postp%:adv%:nomC%>


        .o. %<Prn%:qst%> -> %<Prn%:qst%:pers%> || .#. {kim} _
        .o. %<Prn%:qst%> -> %<Prn%:qst%:pers%> || .#. {hangi} _
        .o. %<N%> -> %<Adj%> %<%0%> %<N%> || .#. {hangi} _
        .o. %<V%> %<part%:pres%> %<Adj%> %<%0%> %<N%> -> %<V%> %<vn%:pres%> %<N%>
        .o. s [e|a] l %<Adj%> -> %<N%> %<sal%> %<Adj%>
        .o. %<%0%> %<N%> -> 0 || %<N%:prop%> _ 
        .o. %<%0%> %<V%> -> %<%0%> %<V%> %<cpl%:pres%> || 
                [%<Exist%>|%<Exist%:neg%>] _ %<3s%> .#.
        .o. %<Exist%:neg%> -> %<Adj%> || y o k _ %<%0%> %<N%>
        ;


regex o2t.i;
save stack o2t.fst

define intSym @"intermediate-symbols.a";
define boundary ⟪MB⟫|⟪RB⟫|⟪IGB⟫|intSym;

! --------------------------------------------------------------------------
! (s)I deletion - the morpheme (s)I is deleted in some contexts.
! What is deleted is  not clear, but we model such that (s)I gets deleted 
! - after another (s)I
! - before lArI
! - after lI, lIk, sIz, CI
! - after -(y)AsI
! - and optionally after 'aynı' and before a case suffix 
! TODO: some of these seems to be optional - at least in real world
!       use. Although 'buzdolaplı' is the correct form, there are many
!       instances of 'buzdoabılı' on the net.
! --------------------------------------------------------------------------

!    ,,  ⟪bs⟫ ⟪I⟫ -> 0 \\ l ⟪I⟫ boundary* _         ! not sure why this was here
!    ,,  ⟪compn⟫ ⟪bs⟫ ⟪I⟫ -> 0 \\ l ⟪I⟫ boundary* _ ! this should not ever happen

regex   ⟪bs⟫ ⟪I⟫ -> 0 \\  ⟪bs⟫ ⟪I⟫ intSym* boundary+ _
     ,, ⟪bs⟫ ⟪I⟫ -> 0 || _ boundary+ [  l (⟪NS⟫) ⟪A⟫ r ⟪I⟫
                                      | l (⟪NS⟫) ⟪A⟫ r boundary+ ⟪bs⟫ ⟪I⟫
                                      | l ⟪I⟫
                                      | l ⟪I⟫ k
                                      | s ⟪I⟫ z
                                      | ⟪C⟫ ⟪I⟫ ] ⟪MB⟫,
                            ⟪by⟫ ⟪A⟫ s ⟪I⟫ boundary+ _ ⟪MB⟫
    ,,  ⟪bs⟫ ⟪I⟫ (->) 0 || .#. {aynı} ⟪RB⟫ _ boundary* [ ⟪by⟫ ⟪A⟫
                                                       | ⟪by⟫ ⟪I⟫
                                                       | ⟪bn⟫ ⟪I⟫ n
                                                       | ⟪D⟫  ⟪A⟫
                                                       | ⟪D⟫  ⟪A⟫ n
                                                       | ⟪by⟫ l   ⟪A⟫ ]
   ,, ⟪bs⟫ (->) 0  || ⟪dels⟫ intSym* boundary* _ ⟪I⟫ ⟪MB⟫
   ,, ⟪dels⟫  -> 0
;

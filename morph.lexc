! morph.lexc - main morphotactic description
!
! Released under the terms of the MIT License
! Copyright (c) 2011-2016 Çağrı Çöltekin <cagri@coltekin.net>
! 
! Permission is hereby granted, free of charge, to any person obtaining a
! copy of this software and associated documentation files (the "Software"),
! to deal in the Software without restriction, including without limitation
! the rights to use, copy, modify, merge, publish, distribute, sublicense,
! and/or sell copies of the Software, and to permit persons to whom the
! Software is furnished to do so, subject to the following conditions:
! 
! The above copyright notice and this permission notice shall be included in
! all copies or substantial portions of the Software.
! 
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
! IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
! FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
! AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
! LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
! FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
! DEALINGS IN THE SOFTWARE.

!
! This file defines an FSA (not an FST) that describes main
! morphological process. The definition here is somwhat "idealized".
! The exceptions are applied as filters.
!

Multichar_Symbols ⟨Adj⟩
                  ⟨Adv⟩
                  ⟨Cnj⟩ 
                    ⟨adv⟩ ⟨coo⟩ ⟨sub⟩
                  ⟨Det⟩
                    ⟨def⟩ ⟨indef⟩
                  ⟨N⟩
                    ⟨pl⟩ ⟨sg⟩
                    ⟨p1s⟩ ⟨p2s⟩ ⟨p3s⟩ ⟨p1p⟩ ⟨p2p⟩ ⟨p3p⟩ ⟨p%0x⟩ 
                    ⟨nom⟩ ⟨acc⟩ ⟨dat⟩ ⟨abl⟩ ⟨loc⟩ ⟨gen⟩ ⟨ins⟩ ⟨siz⟩
                    ⟨ce⟩ ⟨ci⟩ ⟨li⟩ ⟨lik⟩ ⟨sel⟩
                    ⟨ki⟩
                    ⟨apos⟩
                    ⟨len⟩ ⟨les⟩ ⟨le⟩
                    ⟨dim⟩
                    ⟨imsi⟩
                  ⟨Num⟩
                    ⟨ara⟩ ⟨rom⟩ ⟨date⟩ ⟨time⟩
                    ⟨dist⟩ ⟨ord⟩
                  ⟨Pron⟩
                  ⟨V⟩
                    ⟨rfl⟩ ⟨rcp⟩ ⟨caus⟩ ⟨pass⟩
                    ⟨abil⟩ ⟨iver⟩ ⟨adur⟩ ⟨agel⟩ ⟨agor⟩ ⟨akal⟩ ⟨ayaz⟩
                    ⟨neg⟩
                    ⟨aor⟩ ⟨cond⟩ ⟨evid⟩ ⟨fut⟩ ⟨imp⟩ ⟨prs⟩ 
                      ⟨obl⟩ ⟨opt⟩ ⟨past⟩ ⟨prog.yor⟩ ⟨prog.mekte⟩
                    ⟨cpl⟩
                    ⟨1s⟩ ⟨2s⟩ ⟨3s⟩ ⟨1p⟩ ⟨2p⟩ ⟨3p⟩
                    ⟨dir⟩
                    ⟨vn.me⟩ ⟨vn.mek⟩ ⟨vn.yis⟩
                    ⟨part.pres⟩ ⟨part.past⟩ ⟨part.fut⟩ ⟨part.esi⟩ ⟨part.ici⟩
                    ⟨cv.cesine⟩ ⟨cv.ken⟩ ⟨cv.dikce⟩ ⟨cv.eli⟩ ⟨cv.erek⟩
                      ⟨cv.esiye⟩ ⟨cv.inceye⟩ ⟨cv.ince⟩ ⟨cv.ip⟩ ⟨cv.meden⟩
                      ⟨cv.meksizin⟩ ⟨cv.ye⟩
                  ⟨Q⟩ ⟨q⟩
                  ⟨Ij⟩
                  ⟨Alpha⟩
                  ⟨Postp⟩
                  ⟨Sym⟩ ⟨Punc⟩

!
! here we define all the categories that come from the lexicon.
!

LEXICON Root
<@"lexicon/adjective.a">            lex_adjective;
<@"lexicon/adverb.a">               lex_adverb;
<@"lexicon/alpha.a">                lex_alpha;
<@"lexicon/conjunction.a">          lex_conjunction;
<@"lexicon/determiner.a">           lex_determiner;
<@"lexicon/interjection.a">         lex_interjection;
<@"lexicon/noun.a">                 lex_noun;
<@"lexicon/number-alpha.a">         lex_number_alpha;
<@"lexicon/number-arabic.a">        lex_number_arabic;
<@"lexicon/number-date.a">          lex_number_date;
<@"lexicon/number-roman.a">         lex_number_roman;
<@"lexicon/number-time.a">          lex_number_time;
<@"lexicon/postposition.a">         lex_postposition;
<@"lexicon/pronoun.a">              lex_pronoun;
<@"lexicon/punctuation.a">          lex_punctuation;
<@"lexicon/symbol.a">               lex_symbol;
<@"lexicon/verb.a">                 lex_verb;
i                                   lex_cpl_i;
mı                                  lex_question;
mi                                  lex_question;
mu                                  lex_question;
mü                                  lex_question;

!
! This part is only responsible from adding POS tags - lots of repetition :(
!

LEXICON lex_adjective
⟨Adj⟩                   adjective;

LEXICON lex_adverb
⟨Adv⟩                   adverb;

LEXICON lex_alpha
⟨Alpha⟩                 #;
⟨Alpha⟩                 noun_apostrophe;

LEXICON lex_conjunction
⟨Cnj⟩                   #;

LEXICON lex_determiner
⟨Det⟩                   determiner;

LEXICON lex_interjection
⟨Ij⟩                     #;

LEXICON lex_noun
⟨N⟩                     noun;

LEXICON lex_number_alpha
⟨Num⟩                   number;
LEXICON lex_number_arabic
⟨Num⟩⟨ara⟩              number;
LEXICON lex_number_roman
⟨Num⟩⟨rom⟩              number;
LEXICON lex_number_time
⟨Num⟩⟨time⟩             number_datetime;
LEXICON lex_number_date
⟨Num⟩⟨date⟩             number_datetime;

LEXICON lex_pronoun
⟨Pron⟩                  noun;

LEXICON lex_punctuation
⟨Punc⟩                      #;

LEXICON lex_postposition
⟨Postp⟩                     #;
⟨Postp⟩                     adverb;     !TODO: need filter
⟨Postp⟩                     adjective;  !TODO: need filter

LEXICON lex_symbol
⟨Sym⟩                       #;

LEXICON lex_verb
⟨V⟩                     verb;

LEXICON lex_cpl_i
⟨V⟩                     copula;


LEXICON lex_question
⟨Q⟩                     copula;
⟨Q⟩                     pnaggr;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Adjectives
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON adjective
                    #;
                    noun;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Adverbs
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON adverb
                    #;
                    adverb_ca; ! TODO: not all adverbs do this

LEXICON adverb_ca
⟨ce⟩                #;
                    nom_predicate; ! TODO: introduces ambiguity

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Determiners
! determineres normally do not inflect.
! when (most?) determiners are suffixed with a possessive suffix,
! they become pronouns (bir - biri, birimiz)
!
! Since these are a closed class of words, we can have all of them in
! the lexicon as pronouns.
! if commented out, the option below here allows them
! to get the possessive suffixes, and following nominal inflections
! TODO: this should be done a better way
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON determiner
                        #;
!                         determiner_poss;
! 
! LEXICON determiner_poss
! ⟨p1s⟩               noun;
! ⟨p2s⟩               noun;
! ⟨p3s⟩               noun;
! ⟨p1p⟩               noun;
! ⟨p2p⟩               noun;
! ⟨p3p⟩               noun;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Nouns
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! Ordinary nouns
LEXICON noun
                    noun_number;
                    noun_apostrophe;
                    noun_deriv;

LEXICON noun_deriv
⟨sel⟩⟨Adj⟩          adjective;
⟨ce⟩⟨Adv⟩           adverb;     ! TODO: this is more than a derivation
⟨les⟩⟨V⟩            verb;       ! this may also show up in inflected forms
⟨imsi⟩⟨Adj⟩         adjective;
⟨len⟩⟨V⟩            verb;
⟨le⟩⟨V⟩             verb;
⟨dim⟩⟨N⟩            diminutive_cont;

LEXICON diminutive_cont
                    noun_number;
                    noun_apostrophe;

!
! We allow apostrophe to attach any noun here, but filter on 
! the analysis side (TODO)
!
LEXICON noun_apostrophe
⟨apos⟩              noun_number;

! Main morphlogical definition goes below
LEXICON noun_number
⟨pl⟩                noun_poss;
⟨sg⟩                noun_poss;

LEXICON noun_poss
⟨p1s⟩               noun_case;
⟨p2s⟩               noun_case;
⟨p3s⟩               noun_case;
⟨p1p⟩               noun_case;
⟨p2p⟩               noun_case;
⟨p3p⟩               noun_case;
⟨p%0x⟩              noun_case;

LEXICON noun_case
⟨nom⟩               noun_nom;
⟨acc⟩               noun_acc;
⟨dat⟩               noun_dat;
⟨abl⟩               noun_abl;
⟨loc⟩               noun_loc;
⟨gen⟩               noun_gen;
⟨ins⟩               noun_ins;
!
! the following are a list of suffixes that more-or-less fit into
! a case slot.
! these overgenerate, some of them are not possible after
! number/poss -- need filtering.
!
⟨siz⟩               noun_siz;
⟨li⟩                noun_li;
⟨lik⟩               noun_lik;
⟨ce⟩                noun_ca;
⟨ci⟩                noun_ci;


LEXICON noun_nom
                    nom_predicate;
LEXICON noun_acc
                    #;
LEXICON noun_dat
                    nom_predicate;
LEXICON noun_abl
                    nom_predicate;
LEXICON noun_loc
                    nom_predicate;
                    noun_ki;
LEXICON noun_gen
                    nom_predicate;
                    noun_ki;
LEXICON noun_ins
                    nom_predicate;
LEXICON noun_ki
⟨ki⟩⟨Adj⟩           adjective;

LEXICON noun_ca
                    nom_predicate;

LEXICON noun_siz
⟨Adj⟩               adjective;
LEXICON noun_li
⟨Adj⟩               adjective;
LEXICON noun_lik
⟨Adj⟩               adjective;
LEXICON noun_ci
⟨N⟩                 noun;

!
! Nominal predicates
!

LEXICON nom_predicate
⟨cpl⟩⟨V⟩            pnaggr;
⟨cpl⟩⟨V⟩            converb_ken;
⟨cpl⟩⟨V⟩            copula_tam;
⟨q⟩⟨cpl⟩⟨V⟩         pnaggr;
⟨q⟩⟨cpl⟩⟨V⟩         converb_ken;
⟨q⟩⟨cpl⟩⟨V⟩         copula_tam;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Numbers
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON number
                number_ordinal;
                number_distributive;
                noun;

LEXICON number_datetime
                noun;

LEXICON number_ordinal
⟨ord⟩               adjective;

LEXICON number_distributive
⟨dist⟩              number_dist_cont;

LEXICON number_dist_cont
                    #;
⟨li⟩                noun_li;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Verbs
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON verb
                verb_reflexive;

LEXICON verb_reflexive
⟨rfl⟩           verb_causative;
                verb_reciprocal;

LEXICON verb_reciprocal
⟨rcp⟩           verb_causative;
                verb_causative;

LEXICON verb_causative
⟨caus⟩          verb_causative;
⟨caus⟩          verb_passive;
                verb_passive;

LEXICON verb_passive
⟨pass⟩          verb_passive2; ! double passive / impersonal
⟨pass⟩          verb_abil_neg;
                verb_abil_neg;

LEXICON verb_passive2
⟨pass⟩          verb_abil_neg;

LEXICON verb_abil_neg
⟨abil⟩          verb_neg;
                verb_neg;

LEXICON verb_neg
⟨neg⟩           verb_comp;
                verb_comp;


LEXICON verb_comp
                verb_comp_cont;
⟨abil⟩          verb_comp_cont;
⟨iver⟩          verb_comp_cont;
⟨adur⟩          verb_comp_cont;
⟨agel⟩          verb_comp_cont;
⟨agor⟩          verb_comp_cont;
⟨akal⟩          verb_comp_cont;
⟨ayaz⟩          verb_comp_cont;
!⟨akoy⟩

LEXICON verb_comp_cont
                verb_causative;
                verb_tam;
                verb_nonfinite;
!
! The fllowing handles the cases where a compound form can be cuasativized
! (yopıverdirtti), passivized (yapıverildi, or yapıverdirtildi),
! or some compound forms can repeat (yapıverebilirdi).
!
                verb_causative;

LEXICON verb_tam
⟨aor⟩           tam_aor;
⟨cond⟩          question;
⟨evid⟩          tam_evidential;
⟨fut⟩           tam_future;
⟨imp⟩           pnaggr;
⟨prs⟩           pnaggr;
⟨obl⟩           question;
⟨opt⟩           question;
⟨past⟩          question;
⟨prog.yor⟩      question;
⟨prog.mekte⟩    question;

!
! after evidential and future markers, the verb (phrase
! can function as an adjectival. G&K calls this case as 'truncated
! olan' (the auxiliary ol- with -(y)An).
!
LEXICON tam_future
                question;
                adjective;

LEXICON tam_evidential
                question;
                adjective;

!
! aorist is also similar, but this also allows nagative aoris +
! '-dan', '-lık-tan', cases
!
LEXICON tam_aor
                question;
                adjective;

LEXICON question
                pnaggr;
                copula;
⟨q⟩             copula;
⟨q⟩             pnaggr;

!
! copular suffixes and person/number markers - shared by both verbal
! and nominal predicates 
!

LEXICON copula
⟨cpl⟩          converb_ken;
⟨cpl⟩          copula_tam;


LEXICON converb_ken
⟨cv.ken⟩   converb_ken_cont;

LEXICON converb_ken_cont
                adverb;
                question;
                noun_ki;

LEXICON copula_tam
⟨cond⟩         copula_cond2;
⟨evid⟩         copula_evid2;
⟨past⟩         copula_past2;

LEXICON copula_cond2
                    pnaggr;
⟨cpl⟩⟨evid⟩         pnaggr;
⟨cpl⟩⟨past⟩         pnaggr;

LEXICON copula_evid2
                    pnaggr;
⟨cpl⟩⟨cond⟩         pnaggr;
⟨cpl⟩⟨evid⟩         pnaggr;

LEXICON copula_past2
                    pnaggr;
⟨cpl⟩⟨cond⟩         pnaggr;
⟨cpl⟩⟨past⟩         pnaggr;

LEXICON pnaggr
⟨1s⟩            pred_finite;
⟨2s⟩            pred_finite;
⟨3s⟩            pred_finite;
⟨1p⟩            pred_finite;
⟨2p⟩            pred_finite;
⟨3p⟩            pred_finite;

LEXICON pred_finite
                dir;
                converb_cesine;


LEXICON dir
                #;
⟨dir⟩           #;
! TODO: filter


LEXICON verb_nonfinite
⟨vn.me⟩         noun;   ! TODO: need filter
⟨vn.mek⟩        noun;   ! TODO: need filter
⟨vn.yis⟩        noun;
⟨part.pres⟩     adjective;      !-(y)An
⟨part.past⟩     adjective;      !-DIK
⟨part.fut⟩      adjective;      !-AcAK
⟨part.esi⟩      adjective;      !-(y)AsI GK does not list this as 'part'
⟨part.ici⟩      adjective;      !-(y)IcI GK does not list this as 'part'
⟨cv.dikce⟩      adverb;         !-DIkçA
⟨cv.eli⟩        adverb;         !-(y)AlI
⟨cv.erek⟩       adverb;         !-(y)ArAk
⟨cv.esiye⟩      adverb;         !-(y)AsIyA
⟨cv.inceye⟩     adverb;         !-(y)IncAyA
⟨cv.ince⟩       adverb;         !-(y)IncA
⟨cv.ip⟩         adverb;         !-(y)Ip
⟨cv.meden⟩      adverb;         !-mAdAn / -mAzdAn
⟨cv.meksizin⟩   adverb;         !-mAksIzIn
⟨cv.ye⟩         adverb;         !-(y)A requires reduplication 'okuya okuya'

! the suffix -cAsInA attaches in some finite verb configurations.
LEXICON converb_cesine
⟨cv.cesine⟩     adverb;         !-CAsInA TODO: needs filter

